plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.triplet.play' version '2.7.5'
}

android {
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        minSdkVersion 25
        versionCode 290084
        versionName "35.13.2"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        etherealRelease {
            storeFile file("EtherealKey.jks")
            keyAlias "outlinedark"
            storePassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_STORE_PASSWORD
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_KEY_PASSWORD
        }
        outlineRelease {
            storeFile file("OutlineKey.jks")
            keyAlias "outlinekey"
            storePassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_STORE_PASSWORD
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_KEY_PASSWORD
        }
    }

    flavorDimensions  "theme"
    productFlavors {
        dark {
            dimension "theme"
            applicationId "com.schnettler.ethereal"
            signingConfig signingConfigs.etherealRelease
        }
        light {
            dimension "theme"
            applicationId "com.schnettler.outline"
            signingConfig signingConfigs.outlineRelease
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }
}

play {
    serviceAccountCredentials = file("PlayAPI.json")
    track = "production"
}

dependencies {
    def appcompat_version = "1.2.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.cardview:cardview:1.0.0"
}