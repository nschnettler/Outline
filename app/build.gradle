import groovy.io.FileType

import javax.crypto.Cipher
import javax.crypto.SecretKey
import javax.crypto.spec.IvParameterSpec
import javax.crypto.spec.SecretKeySpec
import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext {
    SHOULD_ENCRYPT_ASSETS = true
    SUPPORTS_THIRD_PARTY_SYSTEMS = false

    byte[] key = new byte[16]
    new Random().nextBytes(key)
    KEY = key
    byte[] iv = new byte[16]
    new Random().nextBytes(iv)
    IV_KEY = iv
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 27018
        versionName "Oreo MR - 16.4"

        buildConfigField "boolean", "SUPPORTS_THIRD_PARTY_THEME_SYSTEMS", "" +
                SUPPORTS_THIRD_PARTY_SYSTEMS
        resValue "bool", "SUPPORTS_THIRD_PARTY_THEME_SYSTEMS", "" + SUPPORTS_THIRD_PARTY_SYSTEMS
        resValue "string", "encryption_status", (shouldEncrypt() ? "onCompileVerify" : "false")
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ' - '+ ' (' + getDate() + ')'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        flavorDimensions "theme"
        productFlavors {
            dark{
                dimension "theme"
                applicationId "com.schnettler.ethereal"
                ndk {
                    moduleName "LoadingProcessDark"
                    abiFilters "armeabi-v7a", "arm64-v8a", "x86"
                }
            }
            light {
                dimension "theme"
                applicationId "com.schnettler.outline"
                ndk {
                    moduleName "LoadingProcessLight"
                    abiFilters "armeabi-v7a", "arm64-v8a", "x86"
                }
            }
            minimal {
                dimension "theme"
                applicationId "com.schnettler.minimal"
                ndk {
                    moduleName "LoadingProcessMinimal"
                    abiFilters "armeabi-v7a", "arm64-v8a", "x86"
                }
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.javiersantos:PiracyChecker:1.1'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version") {
        transitive = true
    }
}
def currentFlavor = getCurrentFlavor()

task prepareManifest(){
    def theme = ""
    switch (currentFlavor){
        case "light":
            theme = "outline"
            break
        case "dark":
            theme = "ethereal"
            break
    }
    copy {
        from "/src/main/AndroidManifestTemplate.xml"
        into "/src/main/"
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [theme: theme])
        rename { String fileName ->
           fileName = "AndroidManifest.xml"
        }
    }
}

task backupAssets(dependsOn: prepareManifest) {
    if (shouldEncrypt()) {
        def source = new File(getProjectDir(), "/src/main/assets")
        def backup = new File(getProjectDir(), "/src/main/assets-temp")
        if (!backup.exists()) {
            println("Encryption - Backup - Main Assets")
            copy {
                from source
                into backup
            }
        }
        if (currentFlavor != ""){
            source = new File(getProjectDir(), "/src/" + getCurrentFlavor() + "/assets")
            backup = new File(getProjectDir(), "/src/" + getCurrentFlavor() + "/assets-temp")
            if (!backup.exists()) {
                println("Encryption - Backup - Flavor Assets")
                copy {
                    from source
                    into backup
                }
            }
        }
    }
}

task encryptAssets(dependsOn: backupAssets ) {
    if (shouldEncrypt()) {
        //Get Files to Encrypt
        println("Encryption - Find Files to Encrypt")
        def list = []
        def source = new File(getProjectDir(), "/src/main/assets")
        source.eachFileRecurse(FileType.FILES) { file ->
            list << file
        }
        if (currentFlavor != ""){
            source = new File(getProjectDir(), "/src/" + getCurrentFlavor() + "/assets")
            source.eachFileRecurse(FileType.FILES) { file ->
                list << file
            }
        }


        println("Encryption - Encrypting Files")
        list.each {
            Cipher cipher = Cipher.getInstance("AES/CBC/PKCS5Padding")
            SecretKey secret = new SecretKeySpec(KEY, "AES")
            IvParameterSpec iv = new IvParameterSpec(IV_KEY)

            cipher.init(Cipher.ENCRYPT_MODE, secret, iv)
            FileInputStream fis = new FileInputStream(it)
            FileOutputStream fos = new FileOutputStream(it.getAbsolutePath() + ".enc")

            byte[] input = new byte[64]
            int bytesRead
            while ((bytesRead = fis.read(input)) != -1) {
                byte[] output = cipher.update(input, 0, bytesRead)
                if (output != null) {
                    fos.write(output)
                }
            }
            byte[] output = cipher.doFinal()
            if (output != null) {
                fos.write(output)
            }
            fis.close()
            fos.flush()
            fos.close()
            it.delete()
        }
    }
}

task writeKeys {
    def KEY_READY = String.valueOf("\"" + KEY + "\"")
            .replace("\"", "")
            .replace("[", "{")
            .replace("]", "}")
    def IV_KEY_READY = String.valueOf("\"" + IV_KEY + "\"")
            .replace("\"", "")
            .replace("[", "{")
            .replace("]", "}")

    def headerFile = new File(getProjectDir(), "src/main/jni/LoadingProcess.h")
    headerFile.createNewFile()
    headerFile.text = """
#include <jni.h>
jbyte DECRYPTION_KEY[] = ${KEY_READY};
jbyte IV_KEY[] = ${IV_KEY_READY};
"""
}

project.afterEvaluate {
    preBuild.dependsOn prepareManifest
}



gradle.buildFinished {
    if (shouldEncrypt()){
        println("Encryption - Cleanup")
        def tempAssets = new File(getProjectDir(), "/src/main/assets-temp/")
        def normalAssets = new File(getProjectDir(), "src/main/assets")
        if (tempAssets.exists()) {
            //Delete Encrypted Files
            normalAssets.deleteDir()
            //Restore normal Files
            copy {
                from tempAssets
                into normalAssets
            }
            //Delete Backup Files
            tempAssets.deleteDir()
        }

        tempAssets = new File(getProjectDir(), "/src/" + getCurrentFlavor() + "/assets-temp")
        normalAssets = new File(getProjectDir(), "/src/" + getCurrentFlavor() + "/assets")
        if (tempAssets.exists()) {
            //Delete Encrypted Files
            normalAssets.deleteDir()
            //Restore normal Files
            copy {
                from tempAssets
                into normalAssets
            }
            //Delete Backup Files
            tempAssets.deleteDir()
        }
    }


}

boolean shouldEncrypt() {
    ArrayList<String> tasks = project.gradle.startParameter.taskNames
    return SHOULD_ENCRYPT_ASSETS && Arrays.toString(tasks).toLowerCase().contains("release")
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('dd.MM HH:mm')
    return formattedDate
}

def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String  tskReqStr = gradle.getStartParameter().getTaskRequests().toString()

    Pattern pattern

    if( tskReqStr.contains( "assemble" ) )
        pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
    else
        pattern = Pattern.compile("generate(\\w+)(Release|Debug)")

    Matcher matcher = pattern.matcher( tskReqStr )

    if( matcher.find() )
        return matcher.group(1).toLowerCase()
    else
    {
        println "NO MATCH FOUND"
        return ""
    }
}

repositories {
    mavenCentral()
}